// --------------------------------------------
// Mod setup
// --------------------------------------------
// Basics
BACKUP ~imoenfriendship/backup~
//AUTHOR ~K'aeloree (kae@spellholdstudios.net)~
SUPPORT ~http://www.shsforums.net/forum/599-imoen-friendship/~
VERSION ~v3.6~
README ~imoenfriendship/imoenfriendship-readme-%LANGUAGE%.html~ ~imoenfriendship/imoenfriendship-readme-english.html~

// Translations
AUTO_TRA ~imoenfriendship/translations/%s~

ALWAYS

	/* eet compatibility */
	ACTION_IF GAME_IS ~eet~ BEGIN
		OUTER_SET bg2_chapter = 12
	END ELSE BEGIN
		OUTER_SET bg2_chapter = 0
	END
	OUTER_FOR (i=1; i<=10; i=i+1) BEGIN
		OUTER_SET bg2_chapter = bg2_chapter + 1
		OUTER_SPRINT name_source ~bg2_chapter_%i%~
		OUTER_SET EVAL ~%name_source%~ = bg2_chapter
	END

	/* Converts tra files to UTF-8 */
	// No exception, everything needs to be converted
	ACTION_DEFINE_ARRAY ImFrienoconvert BEGIN END

	// any tra files used in LANGUAGE need to be reloaded after UTF-8 conversion
	ACTION_DEFINE_ARRAY ImFriereload BEGIN Setup END

	LAF HANDLE_CHARSETS
		INT_VAR
			infer_charsets = 1
		STR_VAR
			tra_path = EVAL ~%MOD_FOLDER%/translations~
			noconvert_array = ImFrienoconvert
			reload_array = ImFriereload
	END

END


LANGUAGE ~English~
         ~english~
         ~imoenfriendship/translations/english/setup.tra~
LANGUAGE ~Russian (translation by ArcaneCoast.ru)~
         ~russian~
         ~imoenfriendship/translations/russian/setup.tra~
LANGUAGE ~Polish (translation by Lava Del'Vortel)~
         ~polish~                                       
         ~imoenfriendship/translations/polish/setup.tra~
LANGUAGE ~Francais (traduction : Isaya)~
         ~french~                                       
         ~imoenfriendship/translations/french/setup.tra~         
LANGUAGE ~Espanol (traduccion por Clan DLAN)~
         ~spanish~                                       
         ~imoenfriendship/translations/spanish/setup.tra~     
LANGUAGE ~Chinese (translation by Merzbau)~
         ~schinese~                                       
         ~imoenfriendship/translations/schinese/setup.tra~ 


/* =================================== *
 *    Imoen Friendship for BG2: ToB    *
 * =================================== */

BEGIN @0
REQUIRE_PREDICATE GAME_IS ~bg2ee bgt tob eet~ @1 /* The Imoen Friendship is only compatible with BGII: ToB, BGT and BGII:EE installations. */
DESIGNATED 0 LABEL ~imoen_friendship~

/* CamDawg's custom IsValidForPartyDialogue() state. Thanks Cam!
   covers STATE_CONFUSED, STATE_FEEBLEMINDED, STATE_SILENCED, STATE_DEAD, STATE_ACID_DEATH, STATE_FLAME_DEATH, STATE_EXPLODING_DEATH, STATE_STONE_DEATH, STATE_FROZEN_DEATH, STATE_HELPLESS, STATE_STUNNED, STATE_PANIC, STATE_BERSERK, STATE_SLEEPING */
APPEND ~STATE.IDS~ ~0x80101FEF CD_STATE_NOTVALID~ UNLESS ~CD_STATE_NOTVALID~

// Compiling dialogue & scripts
COMPILE ~imoenfriendship/dialogue/imoen2j.d~
EXTEND_BOTTOM ~imoen2.bcs~ ~imoenfriendship/scripts/imoen2.baf~ EVALUATE_BUFFER
COMPILE ~imoenfriendship/dialogue/imoen25j.d~
EXTEND_TOP ~imoe25.bcs~ ~imoenfriendship/scripts/imoe25.baf~
